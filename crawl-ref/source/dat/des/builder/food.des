##############################################################################
# A number of fruity vaults, by blue_anna
# [Condensed into one vault by HangedMan.]
# [Ruined by wheals.]
#
# TODO: is there a way to retain fruit-like flavor without food?
NAME:   blue_anna_fruit_fields
TAGS:   fruiting_plant allow_dup unrand
KMONS:  P = plant
NSUBST: . = 1:d / *:., " = 2:d / *:.
FTILE:  .dP = floor_grey_dirt / floor_grass w:20 / floor_dirt w:15
COLOUR: .dP = lightgrey / green w:20 / brown w:15
MAP
.""".
."P".
.""".
.....
ENDMAP

NAME:   the_bakery
DEPTH:  D:7-14
# Previously this was a 25% CHANCE; I'm assuming this was meant to weight this
# higher than the other food vaults, which mostly had a 15% CHANCE, but
# placing by chance tag doesn't work that way. Hence, there's also a WEIGHT
# tag to give it more weight than other food vaults.
WEIGHT: 25
: if crawl.one_chance_in(8) then
:   dgn.delayed_decay(_G, 'e', 'spriggan name:baked n_adj n_spe n_des corpse')
: else
SUBST: =e = x
: end
SUBST: = = +
MONS:   spriggan col:white name:baker n_suf ; robe . dagger
MAP
xxxxxx
xdd=ex
x.xxxx
x.1xxx
x+xxxx
ENDMAP

NAME:     blue_anna_alchemist
DEPTH:    D:8-14
WEIGHT: 15
SUBVAULT: X : fruiting_plant
SUBVAULT: W : fruiting_plant
KITEM:    p =  potion of degeneration w:5 / potion of ambrosia w:1 / nothing
MONS:     big kobold ; wand of flame ident:type . short sword
MAP
XXXXX..WWWWW
XXXXX..WWWWW
XXXXX..WWWWW
XXXXX..WWWWW
x.x.c++c.x.x
ccccc..ccccc
cppc....cppc
cp.+.1..+.pc
cccc....cccc
cccccccccccc
ENDMAP

# Not really a fish farm now that fish are gone...
NAME:   hangedman_fish_farm
DEPTH:  D:9-14
WEIGHT: 15
TAGS:   no_monster_gen no_pool_fixup \
        patrolling
KMONS:  A = merfolk ; spear
KFEAT:  g = shallow_water
ITEM:   throwing net
:       dgn.delayed_decay(_G, 'g', 'electric eel corpse')
MAP
xxxxxx
xgxedx
+..fAx
xWWWWx
xxxxxx
ENDMAP

NAME:   hangedman_pestilent_swarm
DEPTH:  D:8-14
WEIGHT: 15
TAGS:   extra no_monster_gen patrolling
MONS:   hornet / worm w:5 / worker ant
MONS:   hornet w:7 / soldier ant w:13
MAP
.xxxxxxx
@xx.x.xcccc
 ..x.x2cddc
@xx.x.x+ddc
.x.x.x2cccc
 xxxxxxx
ENDMAP

NAME:   hangedman_little_slice_of_home
DEPTH:  D:8-14
WEIGHT: 10
KMONS:  A = jelly
KMONS:  B = ooze
: if you.absdepth() > 10 then
KMONS:  C = slime creature
: end
KFEAT:  C = altar_jiyva w:1 / . w:20
KMASK:  W = no_monster_gen
MARKER: + = lua:props_marker({ door_restrict="veto" })
MAP
 .Wxxxxx
.Wxxxxxxx
@.xBBAxxx
 W+BxA+Cx
@.xBBAxxx
.Wxxxxxxx
 .Wxxxxxx
ENDMAP

NAME:     hangedman_ranch
DEPTH:    D:8-14
WEIGHT:   10
TAGS:     no_monster_gen patrolling
SUBVAULT: Z : fruiting_plant
KMONS:    A = three-headed hydra / w:5 two-headed hydra
KMONS:    B = dream sheep
KMONS:    C = yak
KMONS:    D = big kobold
KMONS:    E = big kobold ; nothing
KMONS:    F = plant
KITEM:    E = falchion ego:flaming ident:type / \
              hand axe ego:flaming ident:type
KITEM:    I = halberd
KFEAT:    EF = shallow_water
MAP
cccccccccc
cAH+.....c
cIDcD..BBc
c+ccm+++cc
x.WFZZZZZx
xFWWZZZZZx
xWEFZZZZZx
xFW.ZZZZZx
xFFW.W.W.x
xxxxx@xxxx
ENDMAP

NAME:    hangedman_stone_soup
DEPTH:   D:10-14
WEIGHT:  10
TAGS:    extra patrolling no_rotate no_vmirror
# Branch representatives working together to cook some soup:
# D, Orc, Elf, Swamp, Shoals, Vaults, Crypt, Hells.
KMONS:   A = kobold
KMONS:   B = orc
KMONS:   C = deep elf mage
KMONS:   D = spriggan
KMONS:   E = merfolk
KMONS:   F = human
KMONS:   G = necrophage
KMONS:   H = crimson imp
KITEM:   1 = stone q:1 w:50 no_pickup / large rock q:1 w:24 / \
             mundane hand axe w:1 no_pickup
KITEM:   b = gold / nothing w:12
KFEAT:   1Yab = shallow_water
KMASK:   1Yac = no_monster_gen
MARKER:  Y = lua:fog_machine { cloud_type = "steam", pow_min = 1, \
                 pow_max = 10, delay = 60, size = 1, walk_dist = 0 }
MARKER:  Z = lua:fog_machine { cloud_type = "flame", pow_min = 100, pow_max = 100, \
                 delay = 10, size = 1, walk_dist = 0, excl_rad = 0 }
NSUBST:  c = 2:a / *:b
SHUFFLE: ABCDEFGH
MAP
 .......
..A...B..
..vv.vv..
..vcccv..
..vc1cv..
..vcYcv..
.CvvvvvD.
...Z.Z...
 .......
ENDMAP

# An ogre roasts intruders over his little campfire.
NAME:   grunt_ogre_cave
DEPTH:  D:7-14
WEIGHT: 15
TAGS:   extra no_monster_gen no_trap_gen \
        patrolling
{{
  local twochance = you.absdepth() - 8
  local onechance = 7 - twochance
  if twochance <= 0 then
    mons("ogre")
  elseif onechance <= 0 then
    mons("two-headed ogre")
  else
    mons("ogre w:" .. onechance .. " / two-headed ogre w:" .. twochance)
  end
  dgn.delayed_decay(_G, 'g', 'orc skeleton / human skeleton / elf skeleton'
                             .. ' / dwarf skeleton')
}}
ITEM:   throwing net
MARKER: O = lua:fog_machine { cloud_type = "flame", pow_min = 10, \
                              pow_max = 10, delay = 10, size = 1, \
                              walk_dist = 0, excl_rad = 0 }
MAP
 xxxxx
xxxxxxx
xxeegxx
xx1Ogxx
xxdf.xx
 xxx.xx
 xx.xxx
xxx.xxx
xx.xxx.
 .g.
  @
ENDMAP

NAME:   grunt_livestock
DEPTH:  D:7-14
WEIGHT: 15
TAGS:   extra no_monster_gen no_trap_gen \
        patrolling
MONS:   human ; animal skin . hand axe
{{
  local hog_weight = 14 - you.absdepth()
  if hog_weight <= 0 then
    mons("yak")
  else
    mons("hog w:" .. hog_weight .. " / yak w:1")
  end
}}
KFEAT:  K = iron_grate
KFEAT:  - = open_door
MAP
.xxxxxxx
@+...edx
.x...1dx
 xKK-KKx
 x2...2x
 x.2.2.x
 xxxxxxx
ENDMAP
